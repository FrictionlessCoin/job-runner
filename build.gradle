group = 'com.sequenceiq'
version = '0.1'

apply plugin: 'scala'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    maven { url "http://repo.hortonworks.com/content/repositories/releases" }
    maven { url "https://repository.cloudera.com/artifactory/cloudera-repos" }
    maven { url "http://conjars.org/repo/" }
    maven { url "http://maven.sequenceiq.com/snapshots" }
    maven { url "http://maven.sequenceiq.com/releases" }
}

configurations {
    deployerJars
    all*.exclude group: 'commons-lang', module: 'commons-lang', version: '2.6'
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

tasks.withType(ScalaCompile) {
    targetCompatibility = 1.7
    scalaCompileOptions.deprecation = false
    scalaCompileOptions.unchecked = false
    scalaCompileOptions.additionalParameters = ['-nowarn']
    options.warnings = false
    options.compilerArgs = ["-Xmax-classfile-name 130"]
}

def versionOverrides = [
        "org.apache.hadoop:hadoop-yarn-api": "${hadoopVersion}",
        "org.apache.hadoop:hadoop-yarn-common": "${hadoopVersion}",
        "org.apache.hadoop:hadoop-yarn-server-web-proxy" : "${hadoopVersion}"
]

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "s3://maven.sequenceiq.com/releases") {
                authentication(userName: "$System.env.aws_access_key_id", passphrase: "$System.env.aws_secret_access_key")
            }
            snapshotRepository(url: "s3://maven.sequenceiq.com/snapshots") {
                authentication(userName: "$System.env.aws_access_key_id", passphrase: "$System.env.aws_secret_access_key")
            }
        }
    }
}

configurations.all {

    resolutionStrategy.eachDependency { DependencyResolveDetails details ->

        def overrideVersion = versionOverrides[details.requested.group + ":" + details.requested.name]

        if (overrideVersion != null && details.requested.version != overrideVersion) {
            logger.info "Overriding dependency ${details.requested.group}:${details.requested.name} version ${details.requested.version} --> $overrideVersion"
            details.useVersion overrideVersion
        }
    }
}

dependencies {
    compile "org.scala-lang:scala-library:${scalaVersion}"
    compile("org.apache.hadoop:hadoop-mapreduce-client-core:${hadoopVersion}")
    compile("org.apache.hadoop:hadoop-mapreduce-client-jobclient:${hadoopVersion}")
    compile("org.apache.hadoop:hadoop-common:${hadoopVersion}")
    compile("org.apache.hadoop:hadoop-hdfs:${hadoopVersion}")
    // Ambari
    compile('com.sequenceiq:ambari-client16:1.6.1') {
        exclude group: 'org.slf4j';
    }
    // Cascading & Scalding
    compile( "cascading:cascading-core:${cascadingVersion}")
    compile("com.twitter:scalding-core_2.10:${scaldingVersion}") {
        exclude group: 'cascading';
    }
    // Spark
    compile("org.apache.spark:spark-core_2.10:${sparkVersion}")
    compile("org.apache.spark:spark-yarn_2.10:${sparkVersion}")
    compile("org.apache.spark:spark-mllib_2.10:${sparkVersion}")
    compile("org.apache.spark:spark-bagel_2.10:${sparkVersion}")
    compile("org.apache.spark:spark-repl_2.10:${sparkVersion}")
    compile("org.apache.spark:spark-streaming_2.10:${sparkVersion}")
    compile("org.apache.spark:spark-graphx_2.10:${sparkVersion}")
    compile("org.apache.spark:spark-sql_2.10:${sparkVersion}")
    // Mahout
    compile("org.apache.mahout:mahout-mrlegacy:${mahoutVersion}") { exclude group: 'org.apache.hadoop' }
    // Hadoop
    compile("org.apache.hadoop:hadoop-client:${hadoopVersion}") {
        exclude group: 'javax.servlet';
    }
    deployerJars("org.springframework.build.aws:org.springframework.build.aws.maven:3.0.0.RELEASE")
}
